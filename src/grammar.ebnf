@@grammar :: Obsidian

@@eol_comments :: /#.*?$/

@@whitespace :: /[\t ]+/

program = @:statement_list $ ;

statement_list = {eol | ';'} first:[statement] rest:{separated_statement} {eol | ';'} ;

separated_statement = {eol | ';'}+ @:statement ;

statement = head:block_expression args:{block_expression}* ;

block = {eol} '{-INDENT-}' {eol} @:statement_list {eol} '{-DEDENT-}' ;

block_expression = block_slurp ;

block_slurp = (binary_op)%{simple_expression | block}+ ;

expression = binary_slurp ;

binary_slurp = (binary_op)%{simple_expression}+ ;

binary_op = {eol} @:op {eol}
          | {eol} @:binary_identifier {eol}
          ;

simple_expression = identifier
                  | float
                  | integer
                  | triple_double_string
                  | triple_single_string
                  | interpolated_string
                  | single_string
                  | symbol
                  | curly_expression
                  | call_expression
                  | partial_call_expression
                  | tuple
                  | list
                  | map
                  | unquote_expression
                  ;

curly_expression = '{' {eol} (@+:op | @+:binary_identifier | @+:expression {{eol | ';'} @+:expression}) {eol} '}' ;

call_expression = '(' {eol} head:(expression | op) {eol} args:{call_expression_arg} {eol} ')' ;

partial_call_expression = '[' {eol} head:(expression | op) {eol} args:{call_expression_arg} {eol} ']' ;

call_expression_arg = @:expression {eol} ;

tuple = '(' {eol} ')'
      | '(' {eol} first:expression {eol} ',' {eol} rest:[collection_rest] {eol} ')'
      ;


map = '{' {eol} '}'
    | '{' {eol} first:expression {eol} ',' {eol} rest:[collection_rest] {eol} '}'
    ;

list = '[' {eol} ']'
    | '[' {eol} first:expression {eol} ',' {eol} rest:[collection_rest] {eol} ']'
    ;

collection_rest = @+:expression {{eol} ',' {eol} @+:expression} {eol} [','] ;

unquote_expression = '$' @:simple_expression ;

identifier = /[_a-zA-Z][_a-zA-Z0-9]*[?!]?/ ;
binary_identifier = '~' @:/[_a-zA-Z][_a-zA-Z0-9]*[?!]?/ ;
integer = /-?[0-9][_0-9]*([eE]-?[0-9][_0-9]*)?/ ;
float = /-?[0-9][_0-9]*\.[0-9][_0-9]*([eE]-?[0-9][_0-9]*)?/ ;
triple_double_string = /"""([^"\\]|\\.|"([^"\\]|\\.)|""([^"\\]|\\.))*"""/ ;
triple_single_string = /'''([^'\\]|\\.|'([^'\\]|\\.)|''([^'\\]|\\.))*'''/ ;
single_string = /'([^'\\]|\\.)*'/ ;
symbol = '@' @:/[_a-zA-Z][_a-zA-Z0-9]*[?!]?/ ;

eol = '\n' ;

interpolated_string = '"' @:{STRING_BODY | INTERPOLATED_BODY} '"';
INTERPOLATED_BODY = '{' {eol} @:expression {eol} '}' ;

STRING_BODY = /([^"\\{}]|\\.)+/ ;

op = /[+\-=\/*&|^%!?<>.:]+/ ;
